%: {::m5_load(init.m5.0.2)}

%: {::define_whitespace_handler('replace everywhere in header with ::nwh`)}

%: compile m5_c_quote = { ::c_quote }

%: compile translation_unit = ({::external_declaration} or {::pragma})*
%: compile external_declaration = {::function_definition} or {::declaration}
%: compile function_definition = {::declaration_specifiers }  {::declarator } {::declaration }* {::compound_statement}

%: compile pragma = {#::line_rest }

%: compile declaration_specifiers = ({::storage_class_specifier} or {::type_qualifier})* {::type_specifier} ({::storage_class_specifier} or {::type_qualifier})*

%: compile declaration_specifier = {::storage_class_specifier} or {::type_specifier} or {::type_qualifier}

%: compile storage_class_specifier = {auto}
                        or {register}
                        or {static}
                        or {extern}
                        or {typedef}

%: compile type_specifier = {void}
                   or {char}
                   or {short}
                   or {int}
                   or {long}
                   or {float}
                   or {double}
                   or {signed}
                   or {unsigned}
                   or { ::struct_or_union_specifier}
                   or { ::enum_specifier}
                   or { ::typedef_name}

%: compile struct_or_union_specifier = { ::struct_or_union ::identifier '{` } { ::struct_declaration}+ { '}` } 
                              or { ::struct_or_union '{` } { ::struct_declaration}+ { '}` } 
                              or { ::struct_or_union ::identifier}

%: compile struct_or_union = {struct}
                    or {union}

%: compile struct_declaration = {::specifier_qualifiers ::struct_declarator_list ; }

%: compile specifier_qualifiers = {::type_qualifier }* {::type_specifier} { ::type_qualifier}*
%: compile specifier_qualifier = { ::type_specifier}
                        or { ::type_qualifier}

%: compile struct_declarator_list = { ::struct_declarator} ({ ',` } { ::struct_declarator})*

%: compile struct_declarator = { ::declarator}
                      or { ::declarator} { ':` } { ::constant_expression}
                      or { ':` } { ::constant_expression}

%: compile declarator = ({ ::pointer})? { ::direct_declarator}

%: compile pointer = { '*` } ({ ::type_qualifier})* ({ ::pointer})?

%: compile type_qualifier = {const}
                   or {volatile}
                   or {short} or {long}
                   or {unsigned} or {signed}
                   or {__attribute__::balanced_parens}

%: compile identifier_or_declarator = { ::identifier } or { ( ::declarator ) }

%: compile direct_declarator = { ::identifier_or_declarator } (
       { [ } ({ ::constant_expression})? { ] } 
    or { ( } {::parameter_type_list} { ) } 
    or { ( } ({::identifier})* { ) }
)*
                      
%%: -> {':`} identifier (suppress_error) {_}
%%: -> {':`} identifier (suppress_error) {::aZ}
%%: -> {':`} identifier (suppress_error) {_::alnum_s}
%%: -> {':`} identifier {::aZ::alnum_s}



%: compile COMMENT_138476 = { read c parsing documentation on how to correctly parse c expressions}

%: compile constant_expression = { ::conditional_expression}

%: compile conditional_expression = 
                              { ::logical_or_expression} ({ '?` } { ::expression} { ':` } { ::conditional_expression})?
%: compile strict_conditional_expression =
                              { ::logical_or_expression}  { '?` } { ::expression} { ':` } { ::conditional_expression}

%: compile logical_or_expression = { ::logical_and_expression} ({ '||` } { ::logical_and_expression})*
%: compile strict_logical_or_expression = { ::logical_and_expression} ({ '||` } { ::logical_and_expression})+

%: compile logical_and_expression = { ::inclusive_or_expression} ({ '&&` } { ::inclusive_or_expression})*

%: compile inclusive_or_expression = { ::exclusive_or_expression} ({ '|` } { ::exclusive_or_expression})*

%: compile exclusive_or_expression = { ::and_expression} ({ '^` } { ::and_expression})*

%: compile and_expression = { ::equality_expression} ({ '&` ::equality_expression})*

%: compile equality_expression = { ::relational_expression} (
       { '==` } { ::relational_expression}
    or { '!=` } { ::relational_expression}
)*

%: compile relational_expression = { ::shift_expression} (
       { '<` ::shift_expression}
    or { '>` ::shift_expression}
    or { '<=` ::shift_expression}
    or { '>=` ::shift_expression}
)*

%: compile shift_expression = { ::additive_expression} (
       { '<<` ::additive_expression}
    or { '>>` ::additive_expression}
)*

%: compile additive_expression = { ::multiplicative_expression} (
       { '+` } { ::multiplicative_expression}
    or { '-` } { ::multiplicative_expression}
)*

%: compile strict_additive_expression = { ::multiplicative_expression} (
       { '+` } { ::multiplicative_expression}
    or { '-` } { ::multiplicative_expression}
)+

%: compile multiplicative_expression = { ::cast_expression} (
       { '*` } { ::cast_expression}
    or { '/` } { ::cast_expression}
    or { '%` } { ::cast_expression}
)*

%: compile cast_expression = { ::strict_cast_expression} or { ::unary_expression}

%: compile strict_cast_expression = { '(` } { ::type_name} { ')` } { ::cast_expression}

%: compile unary_expression = { ::postfix_expression}
                     or { '++` } { ::unary_expression}
                     or { '--` } { ::unary_expression}
                     or { ::unary_operator} { ::cast_expression}
                     or {sizeof} { ::unary_expression}
                     or {sizeof} { ::type_name}

%: compile postfix_expression = { ::primary_expression} (
                                   { '[` } { ::expression} { ']` }
                                or { ::argument_list}
                                or { '.` } { ::identifier}
                                or { '->` } { ::identifier}
                                or { '++` } 
                                or { '--` }
                               )*

%: compile primary_expression = { ::identifier}
                       or { ::constant}
                       or { ::string}
                       or { '(` } { ::expression} { ')` } 

%: compile constant = { ::floating_constant} or { ::integer_constant}
             or { ::character_constant}
             or { ::enumeration_constant}
             or { ::string_literal} or { ::char_literal}

%: compile string_literal = { ::m5_c_quote }
%: compile char_literal = {::simple_quote} ({\::anychar} or {::anychar}) {::simple_quote}

%: compile c_sign = {+} or {-}
%: compile floating_constant = ({::int}{.}{::uint}? or {::c_sign}?{.::uint}) ({'f`} or {'d`} or {'F`} or {'D`} or {})
%: compile integer_constant = ({0x} {::hex_char}+) or {::int}
%: compile hex_char = {::09} or {A} or {B} or {C} or {D} or {E} or {F} or {a} or {b} or {c} or {d} or {e} or {f}

%: compile expression = { ::assignment_expression} ({ ',` } { ::assignment_expression})*

%: compile COMMENT_23498 =  {todo: optimize this rule below}
%: compile assignment_expression = { ::unary_expression} { ::assignment_operator} { ::assignment_expression}
                          or { ::conditional_expression} 

%: compile strict_assignment_expression = { ::unary_expression} { ::assignment_operator} { ::assignment_expression}

%: compile assignment_operator = { '=` }
                        or { '*=` } 
                        or { '/=` } 
                        or { '%=` } 
                        or { '+=` } 
                        or { '-=` } 
                        or { '<<=` } 
                        or { '>>=` } 
                        or { '&=` } 
                        or { '^=` } 
                        or { '|=` } 

                       
%: compile unary_operator = { '&` } 
                   or { '*` } 
                   or { '+` } 
                   or { '-` } 
                   or { '~` }
                   or { '!` }






%: compile type_name = {::specifier_qualifiers} ({ ::abstract_declarator})?

%: compile parameter_type_list = { ::parameter_list} { ',` '...` }?

%: compile parameter_list = ({ ::parameter_declaration} { ',` ::parameter_declaration}*)?

%: compile parameter_declaration = ({ ::declaration_specifier})+ { ::declarator}
                          or ({ ::declaration_specifier})+ { ::abstract_declarator}
                          or ({ ::declaration_specifier})+

%: compile argument_list = { '(` } ({ ::assignment_expression,})* { ::assignment_expression} { ')` } or { '(` ')` }

%: compile abstract_declarator = { ::pointer}
                        or { ::pointer} { ::direct_abstract_declarator}
                        or { ::direct_abstract_declarator}

%: compile direct_abstract_declarator =  { '(` } { ::abstract_declarator} { ')` } 
                               ({ '[` } ({ ::constant_expression})? { ']` } 
                               or { '(` } ({ ::parameter_type_list})? { ')` })*

%: compile enum_specifier = {enum} { ::identifier} { '{` } { ::enumerator_list} { '}` } 
                   or {enum} { '{` } { ::enumerator_list} { '}` } 
                   or {enum} { ::identifier}

%: compile enumerator_list = { ::enumerator} ({ ',` } { ::enumerator})*

%: compile enumerator = { ::identifier}
               or { ::identifier} { '=` } { ::constant_expression}

%: compile typedef_name = { ::identifier}

%: compile declaration =  {::declaration_specifiers} { ::init_declarators} { ';` } 

%: compile init_declarators = { ::init_declarator} ({ ',` ::init_declarator})*
%: compile init_declarator = { ::declarator} ({ '=` } { ::initializer})?

%: compile initializer = { ::assignment_expression}
                or { '{` } { ::initializer_list} { '}` } 
                or { '{` } { ::initializer_list} { ',` } { '}` } 

%: compile initializer_list = { ::initializer} ({ ',` } { ::initializer})*




%: compile compound_statement = { '{` } ({ ::declaration} or { ::statement})* { '}` } 

%: compile statement = { ::compound_statement}
              or { ::expression_statement}
              or { ::selection_statement}
              or { ::iteration_statement}
              or { ::jump_statement}
              or { ::labeled_statement}
              or { ::pragma}

%: compile labeled_statement = { ::identifier} { ':` } { ::statement}
                      or {case } { ::constant_expression} { ':` } { ::statement}
                      or {default } { ':` } { ::statement}

%: compile expression_statement = ({ ::expression})? { ';` } 

%: compile selection_statement = ({if } or {switch }) { '(` } { ::expression} { ')` } { ::statement} ({ else ::statement} or !({ else}))

%: compile iteration_statement = {while ( ::expression ) ::statement}
                        or {do ::statement while ( ::expression ) ; } 
                        or {for ( } { ::expression}? { ; } { ::expression}? { ; } { ::expression}? { ) ::statement}

%: compile jump_statement = {goto } { ::identifier} { ';` } 
                   or {continue } { ';` } 
                   or {break } { ';` } 
                   or {return } ({ ::expression})? { ';` }

