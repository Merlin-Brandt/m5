%: {::m5_load(init.m5.0.2)}

# transpile C control structures to bash control structures

%: int_expr {$::alnum_s}
%: int_expr {::int}

%: c_control_structures {
	if ( :a:c_quote == :b:c_quote ) :body:balanced_curly
} -> {
	test :a = :b && :body
}

%: c_control_structures {
	if ((int) :a:int_expr == (int) :b:int_expr) :body:balanced_curly
} -> {
	test :a -eq :b && :body
}

%: nop {}
%::str_expr {::c_quote}
%: str_expr {:v:alnum_s} -> {"$:v"}
%::str_expr_list {}
%: str_expr_list {::str_expr}
%: str_expr_list {:a:str_expr , :r:str_expr_list::nop} -> { :a :r }

%: c_control_structures {
	for ( string :var:alnum_s : {:list:str_expr_list} ) :body:balanced_curly
} -> {
	for :var in :list 
    do
        :body
    done
}

%: use! c_control_structures

age=042

if ("$age" ==  "42") {
    echo "Old enough."
}

if ("$age" == "042") {
    echo "See, this is just a string-wise comparison."
}

amount=0012

if ((int) $amount == (int) 12) {
    echo "See, this is number-wise comparison."
}

if ((int) $amount == (int) 012) {
    echo "See, this is number-wise comparison."
}

file1="path/to/file"
for (string file : {"test.txt", file1, "document.pdf"}) {
    echo "file '$file'"
}

